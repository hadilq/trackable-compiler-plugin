ext.loadAll = { Closure c ->
  file('../gradle.properties').withInputStream {
    Properties props = new Properties()
    props.load(it)
    props.each(c)
  }
}
loadAll{ key, val -> ext.set(key, val) }
buildscript {
  ext.loadAll = { Closure c ->
    file('../gradle.properties').withInputStream {
      Properties props = new Properties()
      props.load(it)
      props.each(c)
    }
  }
  loadAll{ key, val -> ext.set(key, val) }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "com.vanniktech:gradle-maven-publish-plugin:${maven_publish_plugin_version}"
    }
}

apply plugin: "java-gradle-plugin"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: "com.vanniktech.maven.publish"

repositories {
    google()
    jcenter()
}

//region Version.kt template for setting the project version in the build
sourceSets {
    main.java.srcDirs += "$buildDir/generated/sources/version-templates/kotlin/main"
}

TaskProvider copyVersionTemplatesProvider = tasks.register("copyVersionTemplates", Copy) {
    from rootProject.layout.projectDirectory.dir("version-templates")
    into project.layout.buildDirectory.dir("generated/sources/version-templates/kotlin/main")
    expand('projectVersion': "${project.version}")
    filteringCharset = 'UTF-8'
}
//endregion

compileKotlin {
    dependsOn copyVersionTemplatesProvider
}

gradlePlugin {
    plugins {
        trackablePlugin {
            id = 'com.github.hadilq.trackable.trackable-gradle-plugin'
            implementationClass = 'com.github.hadilq.trackable.gradle.TrackableGradlePlugin'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    compileOnly "com.android.tools.build:gradle:3.5.3"

    compileOnly "com.google.auto.service:auto-service-annotations:$autoservice_version"
    kapt "com.google.auto.service:auto-service:$autoservice_version"
}
